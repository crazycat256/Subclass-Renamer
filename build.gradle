plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

def javaFxVersion = '22-ea+16'
def jarBaseName = 'subclass-renamer'

group = 'fr.crazycat256'
version = '2.1'

javafx {
    version = javaFxVersion
    modules = ['javafx.controls', 'javafx.media']
}

application {
    mainClass = 'software.coley.recaf.Main'
}

jar {
    archiveBaseName = jarBaseName
}

// Run Recaf in a dev environment with the plugin
run {
    description = 'Run Recaf with the plugin'

    // Set the Recaf directory to the run directory
    environment 'RECAF', './run'

    // Build the plugin before running Recaf
    dependsOn build

    // Copy the plugin to the run directory
    doFirst {
        delete 'run/plugins'
        copy {
            from 'build/libs'
            into 'run/plugins'
            include jarBaseName + '-' + project.version + '.jar'
        }
    }
    // And then run Recaf
}

// Additional repositories
repositories {
    mavenCentral() // Official repo
    maven { url 'https://jitpack.io' } // 3rd party repo, analyzer
    maven { url 'https://maven.minecraftforge.net/' } // 3rd party repo, Forge
    maven { url 'https://github.com/Col-E/jdk-tools-mirror/raw/master/repository/' } // 3rd party repo, mirror
}

// Dependencies
dependencies {
    compileOnly ('com.github.Col-E:Recaf:dev4-SNAPSHOT') {
        exclude group: 'com.android.tools'
    }

    implementation 'org.kordamp.ikonli:ikonli-carbonicons-pack:12.3.1'
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'com.github.Col-E:tiwulfx-dock:1.2.3'
    implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api:3.0.0-M1'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0-M1'
    implementation 'org.slf4j:slf4j-api:2.0.12'
}

// Additional configurations
configurations {
    runtimeClasspath {
        extendsFrom compileClasspath
    }
}


